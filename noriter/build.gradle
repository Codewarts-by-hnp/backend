buildscript {
	ext {
		queryDslVersion = "5.0.0"
	}
}

plugins {
	id 'org.springframework.boot' version '2.7.4'
	id 'io.spring.dependency-management' version '1.0.14.RELEASE'
	id 'java'
	id 'jacoco'
	id "org.sonarqube" version "3.5.0.2730"
}

group = 'com.codewarts'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	//Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-web'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	//Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	//DB
	runtimeOnly 'mysql:mysql-connector-java'

	//JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// WebClient
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// JWT
	implementation 'com.auth0:java-jwt:4.2.1'

	// Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Query Log
	testImplementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.5.8'

	// QueryDsl
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
	annotationProcessor(
			"javax.persistence:javax.persistence-api",
			"javax.annotation:javax.annotation-api",
			"com.querydsl:querydsl-apt:${queryDslVersion}:jpa"
	)

	//TEST
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.rest-assured:rest-assured'

	// API Docs
	testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'

	// 테스트 환경 lombok 사용
	testCompileOnly 'org.projectlombok:lombok:1.18.24'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

	// TestContainers
	testImplementation "org.testcontainers:junit-jupiter:1.17.2"
	testImplementation "org.testcontainers:mysql:1.17.2"
}

// QueryDSL
sourceSets {
	main {
		java {
			srcDirs = ["$projectDir/src/main/java", "$projectDir/build/generated"]
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}

jar {
	enabled = false
}
bootJar {
	archivesBaseName = 'app'
	archiveFileName = 'app.jar'
	archiveVersion = "0.0.0"
}

jacoco {
	toolVersion = "0.8.7"
}

jacocoTestReport {
	reports {
		xml.enabled true
		csv.enabled true
		html.enabled true
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "PhilSoHanse_backend"
		property "sonar.organization", "philsohanse"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}
